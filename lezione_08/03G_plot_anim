# gnuplot scritp

# setup dell'output
set terminal gif animate delay 50 size 800, 600
set output "plot.gif"

# segna l'asse X alla posizione y=0
set xzeroaxis
set yzeroaxis

# estrae una statistica preventiva dai 'data block' di un file,
# qui serve solo per sapere quanti 'data block' ci sono
# (ovvero quanti sono i passi eseguiti dall'algoritmo ricorsivo)
stats "plot_anim.txt" nooutput

# il primo 'data block' corrisponde alla funzione
# tutti i successivi sono i punti che fanno da intervallo per il metodo adattativo
# questi vengono plottati in un loop
do for [ i = 2:int(STATS_blocks) ] {
	plot	"plot_anim.txt" index 0   using 1:2 title "exact" with lines linetype 6 linewidth 2, \
        "plot_anim.txt" index 1:i using 1:2 title "euler" with linespoints pointtype 3, \
        "plot_anim.txt" index 1:i using 1:3 title "midpoint" with linespoints pointtype 4, \
        "plot_anim.txt" index 1:i using 1:4 title "heun" with linespoints pointtype 5
}